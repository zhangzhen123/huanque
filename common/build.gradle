apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
        arg('eventBusIndex', 'com.julun.huanque.common.CommonEventBusIndex')
    }
}
android {
    compileSdkVersion hq.compileSdkVersion
    buildToolsVersion hq.buildToolsVersion

    defaultConfig {
        minSdkVersion hq.minSdkVersion
        targetSdkVersion hq.targetSdkVersion
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "Integer", "BUILD_NUMBER", "${getBuildNumber()}"
        buildConfigField "String", "IMAGE_SERVER_URL", "\"${api.IMAGE_SERVER_URL}\""
        //正式地址 http
        buildConfigField "String", "SERVICE_BASE_URL_PRODUCT", "\"${api.BASE_URL_PRODUCT}\""
        //测试环境
        buildConfigField "String", "RONGYUN_APP_KEY_TEST", "\"${sdk.GY_KEY_TEST}\""
        //融云key
        buildConfigField "String", "RONGYUN_APP_KEY_PRODUCT", "\"${sdk.GY_KEY_PRODECT}\""
    }


    buildTypes {
        release {
            minifyEnabled false
            manifestPlaceholders = [
                    rongyun_app_key: "${sdk.GY_KEY_PRODECT}"
            ]
            //library配置混淆文件api
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            manifestPlaceholders = [
                    rongyun_app_key: "${sdk.GY_KEY}",
            ]
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def getBuildNumber() {
//如果version.properties文件可读则执行操作
    def versionPropsFile = file('buildnumber.properties')

    if (versionPropsFile.canRead()) {
        //载入version.properties
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        //从version.properties根据关键字"VERSION_CODE"取Integer
        def verCode = versionProps['BUILD_NUMBER'].toInteger()
        //判断build模式，如果是发布模式（':app:assembleRelease'）则自增版本号，并且写回version.properties文件中
        def runTasks = gradle.startParameter.taskNames
        if (':lmapp:assembleRelease' in runTasks) {
            versionProps['BUILD_NUMBER'] = (++verCode).toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        }
        return verCode
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

def frescoVersion = '2.2.0'
def ankoVersion = "0.10.8"
dependencies {
    api fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api rootProject.ext.dependencies["ANDROIDX_KTX"]
    api rootProject.ext.dependencies["ANDROIDX_APPCOMPAT"]
    api 'androidx.constraintlayout:constraintlayout:1.1.3'
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    api 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    api "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    api "androidx.fragment:fragment-ktx:1.2.5"
//    implementation "androidx.webkit:webkit:1.2.0"
    api "androidx.preference:preference-ktx:1.1.1"
    api 'com.google.android.material:material:1.1.0'
    api 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'

    /* anko jetbrains kotlin扩展库*/
    api "org.jetbrains.anko:anko-sdk23:$ankoVersion"
    api "org.jetbrains.anko:anko-sdk23-listeners:$ankoVersion"
    /**网络框架*/
    api "com.squareup.retrofit2:retrofit:2.9.0"
    api "com.squareup.retrofit2:adapter-rxjava3:2.9.0"
//    api 'com.squareup.retrofit2:converter-gson:2.9.0'
    api 'com.google.code.gson:gson:2.8.1'
    //强制使用okHttp3.12.*版本 兼容Android5.0一下
    api("com.squareup.okhttp3:okhttp") {
        version { strictly "3.12.12" }
    }
    api 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    /* 这里注意高版本解析kotlin会报错不能随便升级*/
    api "com.alibaba:fastjson:1.1.57.android"

    api "com.facebook.stetho:stetho-okhttp3:1.5.0"

    debugApi "com.facebook.stetho:stetho:1.5.0"

    /**线程切换*/
    api rootProject.ext.dependencies["RXJAVA"]
    api rootProject.ext.dependencies["RXANDROID"]
    /*生命周期解绑*/
    api 'com.trello.rxlifecycle4:rxlifecycle-components:4.0.0'
    api 'com.trello.rxlifecycle4:rxlifecycle-components-preference:4.0.0'
    api 'com.trello.rxlifecycle4:rxlifecycle-kotlin:4.0.0'
    api 'com.trello.rxlifecycle4:rxlifecycle-android-lifecycle-kotlin:4.0.0'
    /**FRESCO相关*/
    api "com.facebook.fresco:fresco:$frescoVersion"
    api "com.facebook.fresco:animated-base:$frescoVersion"
    api "com.facebook.fresco:animated-gif:$frescoVersion"
    api "com.facebook.fresco:webpsupport:$frescoVersion"
    api "com.facebook.fresco:animated-webp:$frescoVersion"
    api "com.facebook.fresco:drawee-span:$frescoVersion"
    api "com.facebook.fresco:imagepipeline-okhttp3:$frescoVersion"
    /**EVENT_BUS*/
    api "org.greenrobot:eventbus:3.1.1"
    kapt "org.greenrobot:eventbus-annotation-processor:3.1.1"
    /*路由*/
    api rootProject.ext.dependencies["AROUTER_API"]
    api rootProject.ext.dependencies["BASERECYCLERVIEWADAPTERHELPER"]
    kapt rootProject.ext.dependencies["AROUTER_COMPILER"]
    /*启动优化器*/
    api project(':launchstarter')

    /*开源控件*/
    api rootProject.ext.dependencies["MAGICINDICATOR"]
    api "com.github.yyued:SVGAPlayer-Android:2.3.0"
    api "com.github.hackware1993:MagicIndicator:1.6.0"
    api "com.github.yyued:SVGAPlayer-Android:2.3.0"
    api "com.google.android:flexbox:2.0.1"
    //表情切换
    api 'com.effective.android:panelSwitchHelper-androidx:1.3.6'
    api 'com.romandanylyk:pageindicatorview:1.0.1'
    /*阿里云oss*/
    api 'com.aliyun.dpa:oss-android-sdk:2.9.2'
    /* 相册选择 因为很多地方可能会用到 放在common中方便调用*/
    api project(':picture_library')
    api 'com.contrarywind:Android-PickerView:4.1.9'
    api rootProject.ext.dependencies["LOTTIE"]
    api rootProject.ext.dependencies["NINEOLDANDROIDS"]
    //MMKV
    api rootProject.ext.dependencies["MMKV"]
    /* 阿里播放器*/
    api rootProject.ext.dependencies["ALIYUN_PLAYER"]
    api rootProject.ext.dependencies["ALI_VCCONAN"]
    api rootProject.ext.dependencies["AGORA"]
    api 'com.plattysoft.leonids:LeonidsLib:1.3.2'
    api 'com.binioter:guideview:1.0.0'
    api "me.leolin:ShortcutBadger:1.1.22@aar"
    api 'com.google.android.material:material:1.3.0-alpha04'

    api  'com.scwang.smart:refresh-layout-kernel:2.0.1'      //核心必须依赖
    api  'com.scwang.smart:refresh-footer-classics:2.0.1'    //经典加载
    api  'com.scwang.smart:refresh-header-classics:2.0.1'    //经典刷新头
}